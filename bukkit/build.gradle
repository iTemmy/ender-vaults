plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

repositories {
    maven { url = 'https://repo.codemc.org/repository/maven-public/' }
    maven { url = 'https://repo.papermc.io/repository/maven-public/'}
}

dependencies {
    implementation project(':api')
    implementation project(':nms')
    implementation project(':v1_8_R3')
    implementation project(':v1_11_R1')
    implementation project(':v1_14_R1')
    implementation project(':v1_17_R1')
    implementation project(':v1_18_R1')
    implementation project(':v1_19_R2')
    implementation project(':v1_20_R1')
    compileOnly "io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT"
    implementation 'com.zaxxer:HikariCP:5.0.1'
    implementation 'org.mariadb.jdbc:mariadb-java-client:3.1.4'
    implementation 'de.tr7zw:item-nbt-api:2.11.3'
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(19))
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        expand 'pluginVersion': project.ext.version
        duplicatesStrategy = 'include'
        include 'plugin.yml'
    }
}

shadowJar {
    archiveFileName = "EnderVaults-Bukkit-v${project.ext.version}.jar"

    relocate 'de.tr7zw.changeme.nbtapi', 'com.github.dig.endervaults.lib.nbt'
    relocate 'com.zaxxer.hikari', 'com.github.dig.endervaults.lib.hikari'
    relocate 'org.mariadb.jdbc', 'com.github.dig.endervaults.lib.mariadb'
    relocate 'org.javatuples', 'com.github.dig.endervaults.lib.javatuples'
}

build.dependsOn shadowJar